grammar org.xtext.example.Dslproject with org.eclipse.xtext.common.Terminals

generate dslproject "http://www.xtext.org/example/Dslproject"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	agent+=Agent*
;

Agent:
	Entity | Intent
;
	
Intent:
	'Intent' name=ID (':' zuper=[Intent])? (isFollowup=IsFollowup)? question+=Question+ training=Training
;

Entity:
	 'Entity' name=ID '[' example+=EntityExample+ ']'
;

Question: 
	required=Required? questionEntity=QuestionEntity 'with' prompt=Prompt
;

QuestionEntity:
	withEntity=Reference
;

Prompt:
	STRING
;

Required:
	'require'
;

Training:
	{Training} 'Training:' trainingref+=TrainingRef+
	
;

TrainingRef:
	input+=TrainingInput+ '.'
;

TrainingInput:
	{Sin} s=STRING | {Decl} declarations=Declaration
;


Declaration:
	'(' trainingstring=STRING 'is' reference=Reference ')'
;

EntityExample:
	name=ID ','? //Doesn't need to be name=ID (cannot use spaces)
;

IsFollowup:
	'IsFollowup' intent=[Intent]
;

Sysvariable:
	value=('number' | 'date-time' | 'date' | 'duration' | 'address' | 'email' | 'phone-number' | 'date-period' 
		| 'time-period' | 'url' | 'any' | 'color' | 'language'
	)
;

Reference:
	entity=[Entity] | sysvar=Sysvariable
;
