grammar org.xtext.example.Dslproject with org.eclipse.xtext.common.Terminals

generate dslproject "http://www.xtext.org/example/Dslproject"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	agent+=Agent*
;

Agent:
	Entity | Intent | FollowRelation
;
	
Intent:
	'Intent' name=ID ('extends' zuper=[Intent])? question+=QuestionRef+ training=Training
;

Entity:
	 'Entity' name=ID '[' example+=EntityExample+ ']'
;

QuestionRef:
	question=Question | superQuestion=SuperQuestion
;

Question: 
	required='require'? name=ID? '='? questionEntity=QuestionEntity 'with' prompt=STRING
;

SuperQuestion:
	'get' superQuestion=[Question]
;

QuestionEntity:
	withEntity=Reference
;

Training:
	{Training} 'Training:' trainingref+=TrainingRef+
	
;

TrainingRef:
	input+=TrainingInput+ '.'
;

TrainingInput:
	{Sin} s=STRING | {Decl} declarations=Declaration
;


Declaration:
	'(' trainingstring=STRING 'is' reference=Reference ')'
;

EntityExample:
	name=ID ','? //Doesn't need to be name=ID (cannot use spaces)
;

Sysvariable:
	value=('number' | 'date-time' | 'date' | 'duration' | 'address' | 'email' | 'phone-number' | 'date-period' 
		| 'time-period' | 'url' | 'any' | 'color' | 'language'
	)
;

Reference:
	entity=[Entity] | sysvar=Sysvariable
;

FollowRelation:
	'(' parent=[Intent] '->' child=[Intent] ')'
;